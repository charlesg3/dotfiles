HISTFILE=~/.histfile
HISTSIZE=1000
SAVEHIST=1000
setopt HIST_FIND_NO_DUPS

zstyle :compinstall filename '$HOME/.zshrc'

# Enable color support for ls
if [[ "$OSTYPE" == "darwin"* ]]; then
    alias ls='ls -G'
else
    alias ls='ls --color=auto'
fi

# ${(%):-%N} resolves to the current sourced file; walk up zsh/ -> dotfiles/
source "${${(%):-%N}:A:h:h}/shell/colors.sh"
source "${${(%):-%N}:A:h:h}/shell/aliases.sh"

[[ "$OSTYPE" == "darwin"* ]] && fpath=(/opt/homebrew/share/zsh/site-functions $fpath)
autoload -Uz compinit
compinit

# Prompt (colors from shell/colors.sh → ZSH_PROMPT_* variables)
# %n = username, %m = hostname, %~ = current working directory
setopt PROMPT_SUBST
PROMPT='%(?.${ZSH_PROMPT_OK}.${ZSH_PROMPT_ERR})▸${ZSH_PROMPT_RESET}${ZSH_PROMPT_USER}%n${ZSH_PROMPT_SEP}@${ZSH_PROMPT_HOST}%m${ZSH_PROMPT_FG}:${ZSH_PROMPT_DIR}%~ ${ZSH_PROMPT_DOLLAR}\$${ZSH_PROMPT_RESET} '

export PATH="$HOME/.local/bin:$HOME/src/dotfiles/scripts:$PATH"

test -e "${HOME}/.iterm2_shell_integration.zsh" && source "${HOME}/.iterm2_shell_integration.zsh" || true

# zsh-autosuggestions: use lime green for history match suggestions
ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE="fg=${PANDA_LIME}"

# zsh plugins (must be sourced after compinit; syntax-highlighting must be last)
_source_if() { [ -f "$1" ] && source "$1"; }
_source_if "$(brew --prefix 2>/dev/null)/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
_source_if "/usr/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
_source_if "$(brew --prefix 2>/dev/null)/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
_source_if "/usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
unset -f _source_if

# ── Kitty shell integration ───────────────────────────────────────────────────
# $KITTY_INSTALLATION_DIR is set by Kitty at launch; no-op outside Kitty
[[ -n "$KITTY_INSTALLATION_DIR" ]] && \
    source "$KITTY_INSTALLATION_DIR/shell-integration/zsh/kitty.zsh"

# ── Terminal title (populates b:term_title in nvim's buffer list) ─────────────
# Kitty's shell integration handles titles for Kitty tabs; this covers nvim terminals.
if [[ -n "$NVIM" ]]; then
    _title_preexec() { printf '\033]0;%s\007' "$1" }
    _title_precmd()  { print -Pn '\033]0;%~\007' }
    preexec_functions+=(_title_preexec)
    precmd_functions+=(_title_precmd)
fi

# ── Long-command bell (shows green dot in kitty tab) ─────────────────────────
_long_cmd_start=0
_long_cmd_preexec() { _long_cmd_start=$SECONDS }
_long_cmd_precmd() {
    (( _long_cmd_start > 0 && SECONDS - _long_cmd_start >= 10 )) && printf '\a'
    _long_cmd_start=0
}
preexec_functions+=(_long_cmd_preexec)
precmd_functions+=(_long_cmd_precmd)

# ── Local overrides (machine-specific, not tracked) ──────────────────────────
[ -f "$HOME/.zshrc.local" ] && source "$HOME/.zshrc.local"
